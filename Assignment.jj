PARSER_BEGIN(Assignment)
import java.util.*;

public class Assignment {

	//Context for checking rules of the PLM program
	public static List<PLMFunction> functions = new ArrayList<PLMFunction>();
	//The stack of calls being made in the program, used to evaluate nested functions
	public static Stack<FunctionCall> callStack = new Stack<FunctionCall>();
	//A collection of all calls made in the program, used for context and evaluation...
	public static Set<FunctionCall> functionCalls = new HashSet<FunctionCall>();

    public static void main(String args [])   {
        //Reading in the PLM code to be parsed
		Assignment parser = new Assignment(System.in);
		

		//Run the starting product rule, and catch any errors that are thrown in the product rules
		try {
			parser.PLMProgram();
		} catch (ParseException pe) {
			System.out.println("FAIL");
			// System.err.println(pe.currentToken.beginLine);
			System.err.println(pe.getMessage());
			System.exit(0);
		}

		System.out.println("PASS");

		// for (PLMFunction func : functions) {
		// 	System.out.println(func.name + "(" + func.parameter + ") := " + func.body);
		// }

		// System.out.println("------");

		for (PLMFunction func : functions) {
			if (func.name == "MAIN") {
				//Run the MAIN function without an parameter/argument definition, it doesnt have one...
				System.out.println(func.evaluate(0));
			}
		}

	}
}

/**
 * PLM function bodies are a construction of items, all of which are nodes in the program AST
 * which can be evaluated() to determine the value of the function. Three types of Item
 * exist within a PLM program, these are: INTEGER_LITERAL, PARAMETER, FUNCTION_CALLS
 * therefore an interface is implemented to allow for the productions of such nicely.
 */
interface Item {
	//Evaluate the Item to produce an integer - which in the PLM context are only ever positive integers
	//Need to store the valid parameter everytime because context not provided by default
	public int evaluate(int argValue);
}

/**
 * PLMFunction - A function within the PLM language
 */
class PLMFunction {
	//The name of the function, this is either MAIN or a user defined UPPERCHAR string
	public String name;
	//When the function is user defined it has a parameter, a lowercase string - MAIN FUNCS NULL
	public String parameter;
	//The body of the function which is an expression, as defined in the grammar below.
	public Item body;

	public PLMFunction(String name, String parameter, Item body) {
		this.name = name;
		this.parameter = parameter;
		this.body = body;
	}

	public int evaluate(int argValue) {
		//This is essentially calling evaluate on the root of a subtree of the AST
		//This will cause the expression Items to be evaluated individually and built back up
		return this.body.evaluate(argValue);
	}

	public String toString() {
		return "[FUNCTION] name=" + name + " parameter=" + parameter + " body=" + body;
	}
}

class IntegerLiteral implements Item {
	//The actual integer being represented by the object
	public int val;

	public IntegerLiteral(int val) {
		this.val = val;
	}

	//Just return the value of the object, this is just a number nothing special needed.
	//The argument validParameter only used for more complex scenarios
	public int evaluate(int argValue) {
		return this.val;
	}

	public String toString() {
		return Integer.toString(val);
	}
}


class Parameter implements Item {
	public String param;

	public Parameter(String param) {
		this.param = param;
	}

	public int evaluate(int argValue) {
		return argValue;
	}

	public String toString() {
		return param;
	}
}

class FunctionCall implements Item {
	public String functionName;
	public Item argument;

	public FunctionCall(String functionName, Item argument) {
		//System.out.println("Call to function " + functionName + " made with argument " + argument);
		this.functionName = functionName;
		this.argument = argument;
	}

	public int evaluate(int argValue) {
		//Divergence check is done here, we need to make sure no identifcal calls are made previously on the stack
		int positionOfCall = Assignment.callStack.search(this);
		if (positionOfCall != -1) {
			//This call has been made before, this is a divergence...
			System.out.println("DIVERGENCE");
			System.exit(0);
		}

		//This is not a recursive loop, thank god...
		Assignment.callStack.push(this);

		//Evaluate all of the arguments within the function call argument
		int evaluatedArg = argument.evaluate(argValue);

		

		PLMFunction foundFunc = null;
		for (PLMFunction func : Assignment.functions) {
			if (func.name.equals(this.functionName)) {
				foundFunc = func;
				break;
			}
		}
		int functionEvaluation = foundFunc.evaluate(evaluatedArg);
		
		//This function will be at the top of the stack once its evaluated, hopefully.
		//Remove this so we can continue and get no recursion errors
		Assignment.callStack.pop();

		return functionEvaluation;
	}

	public String toString() {
		return "FUNCTION CALL: " + functionName;
	}
}

/**
 * Multipurpose expression class which represents both multiplication and addition.
 */
enum ExpressionType {
	MULTIPLICATION,
	ADDITION
}

class Expression implements Item {
	public Item x;
	public Item y;
	public ExpressionType type;

	public Expression(Item x, Item y, ExpressionType type) {
		this.x = x;
		this.y = y;
		this.type = type;
	}

	//Essentially performing an in-order traversal of the AST to evaluate expressions
	public int evaluate(int argValue) {
		if (this.type == ExpressionType.MULTIPLICATION) {
			return x.evaluate(argValue) * y.evaluate(argValue);
		} else {
			return x.evaluate(argValue) + y.evaluate(argValue);
		}
	}

	public String toString() {
		if (this.type == ExpressionType.MULTIPLICATION) {
			return x + "*" + y;
		} else {
			return x + "+" + y;
		}
	}
}
PARSER_END(Assignment)

/**
 *  The grammar for the assignment is essentially the following:
 * 		plmprogram = ( function )+
 * 		function = DEF (MAIN | UPPERCHARS lowerparam) { expression } ;
 * 		expression = term(+ term)*
 * 		term = item(multiplication item)*
 * 		item = INTEGER_LITERAL | PARAMETER | UPPERCHARS(expression)
 * 
 * 
 * 	The expression calculates the addition first because this means that the products
 * 	which have higher precedence are calculated first in the AST
 * 
 * 	As JJTree is disallowed you cannot make rules which work as AST nodes and let it calculate
 * 	for you, therefore I will need to replicate this behaviour myself with a stack etc...
 * 	Information on how JJTree could be used here: https://www.cs.nmsu.edu/~rth/cs/cs471/InterpretersJavaCC.html
 * 	My solution is based on replicating this myself
 * 
 * 	The program has no built-in context, therefore java util objects will need to be used
 * 	to store data, allowing for checks to be made where context is needed.
 */


//Tokens representing constitutent parts of the prograTOKEN: { <LEFTBRACKET: "(" > }
TOKEN: { <LEFTBRACKET: "(" > }
TOKEN: { <RIGHTBRACKET: ")" > }
TOKEN: { <LEFTCURLYBRACKET: "{" > }
TOKEN: { <RIGHTCURLYBRACKET: "}" > }
TOKEN: { <SPACE: " " > }
TOKEN: { <DEF: "DEF" > }
TOKEN: { <MAIN: "MAIN" > }
TOKEN: { <FUNCNAME: ( <UPPERCHAR> )+ > }
TOKEN: { <PARAMETER: ( <LOWERCHAR> )+ > }
TOKEN: { <INTEGER_LITERAL: ( <DIGIT> )+ > }
TOKEN: { <SEMICOLON: ";" > }
TOKEN: { <EOL: ("\n" | "\r\n" | "\r") > }

TOKEN: { <ADDITION: "+" > }
TOKEN: { <MULTIPLICATION: "*" > }

TOKEN: { <UPPERCHAR: ["A"-"Z"] > }
TOKEN: { <LOWERCHAR: ["a"-"z"] > }
TOKEN: { <DIGIT:     ["0"-"9"] > }


/**
 * The PLM language is a sequence of one or more PLM functions, where one must be the main function.
 * This is defined as below with context checks done in the Assignment class to ensure
 * no duplicates and MAIN is defined.
 */
void PLMProgram(): { PLMFunction plmFunc; }
{
	( 
		plmFunc = FunctionDef()
		//Each time a function is read, we store it in the function list for context purposes
		{ functions.add(plmFunc); }
	)+
	<EOF>
}

/**
 * PLM Functions are the constituent parts of a PLM program, these are either user defined
 * or the main function, which has no parameter. These are defined as such
 */
PLMFunction FunctionDef(): { Token funcName; Token parameter; String parameterString = ""; Item additiveExpression; }
{
	<DEF> <SPACE>
	(
		funcName = <MAIN>
		|
		funcName = <FUNCNAME> <SPACE> 
		parameter = <PARAMETER>
		{ parameterString = parameter.image; }
	)
	<SPACE> 
	<LEFTCURLYBRACKET>
	<SPACE>

	//The function body is an additive expression therefore we match for that here.
	//This has its own "complex" production rules because it has many varieties.
	additiveExpression = MatchAdditiveExpression(parameterString)
	
	<SPACE>
	<RIGHTCURLYBRACKET>
	<SPACE>
	<SEMICOLON>
	<EOL>

	{ return new PLMFunction(funcName.image, parameterString, additiveExpression); }
}

/**
 * 3 types of terms exist within a PLM program, these are:
 *  - INTEGER_LITERAL
 *  - PARAMETER
 *  - FUNCTION_CALL
 * 
 * 	This production matches the term with the type and generates the correct
 * 	AST object so it can be evaluated later in the program
 */
Item MatchItem(String validParameter): { Token token; Item argumentExpression; }
{	
	//INTEGER LITERAL ITEM MATCHING
	token = <INTEGER_LITERAL>
	{ return new IntegerLiteral(Integer.parseInt(token.image)); }

	|

	//PARAMETER ITEM MATCHING
	token = <PARAMETER>
	{ return new Parameter(token.image); }

	|

	//FUNCTION CALL ITEM MATCHING
	token = <FUNCNAME> <LEFTBRACKET> argumentExpression = MatchAdditiveExpression(validParameter) <RIGHTBRACKET>
	{ 
		//Make a new call to this function with the arguments it has been given in the matching.
		FunctionCall fCall = new FunctionCall(token.image, argumentExpression);
		Assignment.functionCalls.add(fCall);
		return fCall;
	}

}


/**
 * The structure of a function body is an expression, we match the LHS, either an
 * multiplication expression or an item and then we pass that on to the sum matcher.
 */
Item MatchAdditiveExpression(String validParameter): { Item bodyExpression;}
{
	bodyExpression = MatchProductExpression(validParameter)
	bodyExpression = MatchSum(validParameter, bodyExpression)
	{
		return bodyExpression;
	}
}

/**
 * The matching of an item operand and a multiplicative operator.
 */
Item MatchProductExpression(String validParameter): { Item multiplicativeExpression; }
{
	multiplicativeExpression = MatchItem(validParameter)
	multiplicativeExpression = MatchProduct(validParameter, multiplicativeExpression)
	{
		return multiplicativeExpression;
	}
}

/**
 * Already have an ITEM/MultiplicativeExpression so want to match with a + and another Item
 */
Item MatchSum(String validParameter, Item lhsItem): { Item sum; }
{
	( <ADDITION> sum = MatchProductExpression(validParameter) 
		{ lhsItem = new Expression(lhsItem, sum, ExpressionType.ADDITION); }
	)*

	{
		return lhsItem;
	}
}

/**
 * Already have an ITEM/MultiplicativeExpression so want to match with a + and another Item
 */
Item MatchProduct(String validParameter, Item lhsItem): { Item multiply; }
{
	( <MULTIPLICATION> multiply = MatchItem(validParameter) 
		{ lhsItem = new Expression(lhsItem, multiply, ExpressionType.MULTIPLICATION); }
	)*

	{
		return lhsItem;
	}
}